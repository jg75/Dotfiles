#! /bin/bash
# git-prompt
# James G.

git status --branch \
           --porcelain 2> /dev/null |
awk -v quote="'" \
   -v branchGraphic=$'\xEE\x82\xA0' \
   -v separator=$'\xE2\x9A\xA1' \
   -v behindChar=$'\xEF\xBF\xA9' \
   -v aheadChar=$'\xEF\xBF\xAB' 'BEGIN{
    alert = 0
    behind = 0
    ahead = 0
    modified = 0 
    txtValue = ""
}{
    if (NR == 1) {
        gsub(/\[/, "")
        gsub(/\]/, "")
        gsub(/,/, "")

        if ($2 ~ /^.+\.\.\..+$/) {
            branch = substr($2, 1, index($2, "...")-1)
            upstream = substr($2, index($2, "...")+3)
        } else {
            branch = $2
        }

        if (NF > 2) {
            for (i = 3; i <= NF; i+=2) {
                j = i+1

                if ($i == "behind") {
                    behind = $j
                } else if ($i == "ahead") {
                    ahead = $j
                }
            }
        }
    } else if (NR > 1) {
        modified += 1
    }
}END{
    if (upstream ~ /^.+$/) {
        txtValue = upstream
    } else if (branch ~ /^.+$/) {
        txtValue = branch
    }

    if (behind > 0) {
        txtValue = sprintf("%s%s%s", behind, behindChar, txtValue)
    }

    if (ahead > 0) {
        txtValue = sprintf("%s%s%s", txtValue, aheadChar, ahead)
    }

    if (modified > 0) {
        txtValue = sprintf("%s%s  %s", txtValue, separator, modified)
    }

    if (length(txtValue) > 0) {
        printf("%s %s\n", branchGraphic, txtValue)
    }
}'

